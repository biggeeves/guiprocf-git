PROCEDURE      GUI_MAIN.START:T  (158208,39586)  |Last amended Dec 11, 2015,10:59:46
C It's best to call GUIPROCF.SYSTEM.START
c but if the file attribute for the GUI_PROCF is defined you can start from this file
C CALL this member to start the guiprocf from a database.

C IF GUI_PROCF_RC = 1, USE THE RELEASE CANIDATE.  IF NOT PROCEED WITH THE USUAL GUI.

C CHECK IF THE REQUIRED GUIPROCF ATTRIBUTE IS SET
C IF THE ATTRIBUTE IS NOT SET ASK USER FOR LOCATION.
C IF FILE DOES NOT EXISTS SET GLOBAL TO NOT CONTINUE

GLOBAL GO_ON = 1   | THIS GLOBAL IS ONLY USED HERE.  VERY IMPORTANT GLOBAL IN THE BEGINNING

c check to see that GUIPROCF is defined
program
. integer * 1 DUMMY
. ifthen( exists( dsn( 'GUIPROCF' ) ) EQ 0 )
.   execute dbms "call TOOLS.SET_GUIPROCF_ATTRIBUTE"
.   write 'ATTRIBUTE NOT SET CALL SET ATTRIBUTE'
. endif
. ifthen( fileis( dsn( 'GUIPROCF' ) ) ne 1 )
.   execute dbms "call TOOLS.SET_GUIPROCF_ATTRIBUTE"
.   write 'FILE DOES NOT EXISTS. CALL SET ATTRIBUTE'
. endif
end program

c must exit program to set guiprocf attribute

program
. integer * 1 DUMMY
C Check to see if GUIPROCF is defined
. ifthen( exists( dsn( 'GUIPROCF' ) ) EQ 0 )
.   DUMMY = globaln( 'GO_ON', 0)
.   write "The attribute 'GUIPROCF' must be defined."
. endif
. ifthen( fileis( dsn( 'GUIPROCF' ) ) ne 1 )
.   DUMMY = globaln( 'GO_ON', 0)
.   write "The attribute 'GUIPROCF' must be point to a real file."
. endif
end program

c There are two versions: The live version and the Release Canidate.
c Set attritute to Release canidate if the global GUI_PROCF_RC=1
call GUIPROCF.TOOLS.CHECK_VERSION

C This comment makes sure attribute has time to take effect.

C A DATABASE MUST BE ATTACHED OR THIS WON'T WORK
PROGRAM
. integer * 1 DUMMY
. ifthen ( system( 39 ) lt 1 )
.   DUMMY = globaln( 'GO_ON', 0)
.   write // 'You must connect a database before you continue.'//
. endif
end program

CIF EQ 0<GO_ON>, 1
c User must agree to the terms
PROGRAM CRWARN
. CALL GUIPROCF.GUI_MAIN.DECLARE_VARIABLES
. EXECUTE SUBPROCEDURE SET_PROCFILE_VARS
. ifNOTthen ( sglobal('USER_AGREEMENT' ) = 'Y' ) | NO USER AGREEMENT
.   ifthen( meminfo( DB_PROC_FILE + '.GUI_CUSTOM.USER_AGREEMENT:T', 1 ) = MEM_T )
.     COMSTR = "CALL " + DB_PROC_FILE + ".GUI_CUSTOM.USER_AGREEMENT"
.   else
.     COMSTR = "CALL GUIPROCF.GUI_CUSTOM.USER_AGREEMENT"
.   ENDIF
.   EXECUTE DBMS COMSTR | SHOW USER AGREEMENT
|     CHECK UPDATED USER_AGREEMENT GLOBAL
.   ifNOTthen( sglobal( 'USER_AGREEMENT' ) EQ 'Y' )
.     DUMMY = globaln( 'GO_ON', 0)
.     write // 'You must confirm the user agreement before continuing.'//
.   endif
. endif
CALL GUIPROCF.GUI_MAIN.SET_PROCFILE_VARS
end program
cif end


C START THE GUI

CIF EQ 0<GO_ON>, 1
. CALL GUIPROCF.GUI_MAIN.ALL_COMMANDS
CIF FALSE
. PROGRAM
.   WRITE // 'CAN NOT START MAIN GUI.' //
. END PROGRAM
CIF END
END PROCEDURE
