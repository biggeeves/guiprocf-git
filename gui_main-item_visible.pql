PROCEDURE      GUI_MAIN.ITEM_VISIBLE:T  (158330,50192)  |Last amended Apr 11, 2016,13:56:32
subprocedure ITEM_VISIBLE
| first section is the show/hide portion.
| defaults for every page

| Default HIDDEN
. hide item CASE_BREAD_CRUMB
. hide item PAGE_DELETE_CASE
. hide item PAGE_DBA
. hide item REC_FORM_SELECTED
. hide item REC_TYPE_SELECTED
. hide item SCREEN_LABEL
. hide item TIME_LABEL
. hide item TABEL_LABEL

| Default VISIBLE
. show item LOG_OUT

| Case Database Specific
. ifthen (dbtype(0) eq 1)
.   show item PAGE_CASEIDS
.   enable item PAGE_CASEIDS
. else
.   hide item CASE_BREAD_CRUMB
.   hide item PAGE_CASEIDS
. endif



c GUI PROCFILE: LIVE OR RELEASE CANDIDATE.
. execute subroutine SYSPROC.TOOLS.FILENAME (dsn('GUIPROCF')) RETURNING (PATH_FILE,NAME_FILE,EXT_FILE)
. ifthen(upper(trim(NAME_FILE)) EQ 'GUIPROCF-RC')
.   check item USE_GUI_RC
.   uncheck item USE_GUI_PG
. else
.   check item USE_GUI_PG
.   uncheck item USE_GUI_RC
. endif

. ifthen (sglobal('COMMENT') eq '.')
.   check item COMMENT_SHOW
. endif

. ifthen (sglobal('DEBUG_MODE') eq '1')
.   check item TURN_DEBBUGER_ON
. endif

. ifthen (sglobal('SHOW_DEBUG_INFO') eq '1')
.   check item POP_UP_DEBUG_INFO
. endif

c page dependent items
c ------  Home Page -------------------
. ifthen (m_page = VIEW_HOME)
.   focus item CASE_SEARCH
.   disable item PAGE_CASEIDS

c   case list
.   ifthen(I_GUI_CASE_LIST EQ 1)
.     show item PAGE_LIST_CASES
.   else
.     hide item PAGE_LIST_CASES
.   endif

C   NEW CASE BUTTON
.   ifthen(I_GUI_CASE_NEW EQ 1)
.     ifthen (dbtype(0) eq 1)
.       show item CASE_NEWB
.     endif
.   else
.     hide item CASE_NEWB
.   endif

C   DELETE CASE BUTTON
.   ifthen (dbtype(0) eq 1)
.     if(I_GUI_DBA = 1) show item PAGE_DELETE_CASE
.     if(I_GUI_CASE_DELETE = 1) show item PAGE_DELETE_CASE
.   endif
.   if (I_GUI_DBA = 1) show item PAGE_DBA

c   Report Button
.   ifthen( I_GUI_REPORTS eq 1 )
.     show item PAGE_CASE_SUMMARY
.   else
.     hide item PAGE_CASE_SUMMARY
.   endif

c   Database Lock Status
.   execute subprocedure SHOW_DB_LOCK_STATUS


c -------------------------------------
c ------  Case List Page --------------
. elseif (m_page = VIEW_LIST_CASES)
.   show item DCC_LOGO


c -------------------------------------
c ------  Delete a CASE ID Page -------

. elseif (m_page = VIEW_CASE_DELETE_SUMMARY)
.   hide item CASE_BREAD_CRUMB


c -------------------------------------
c ------  Forms Page-------------------

. elseif (m_page = VIEW_OLD_KEYS )  | Main page for speicific case ID
.   focus item FORM_LIST
.   ifthen (dbtype(0) eq 1)
.     show item CASE_BREAD_CRUMB
.   else
.     hide item CASE_BREAD_CRUMB
.   endif
C   Case Memo Button
.   ifthen( exists( recnum ('CASE_MEMO') ) eq 0 )
.     hide item GOTO_CASE_MEMO
.   endif

c ------  New Keys --------------------

. elseif( m_page = VIEW_NEW_KEYS )
.   ifthen( dbtype(0) eq 1 )
.     show item CASE_BREAD_CRUMB
.   else
.     hide item CASE_BREAD_CRUMB
.   endif

c ------  Case Summary ----------------
. elseif ( m_page = VIEW_CASE_SUMMARY )
.   ifthen ( dbtype(0) eq 1 )
.     show item CASE_BREAD_CRUMB
.   else
.     hide item CASE_BREAD_CRUMB
.   endif

c ------  All Keys In Table -----------
. elseif ( m_page = VIEW_TABLE )
.   ifthen ( dbtype(0) eq 1 )
.     show item CASE_BREAD_CRUMB
.   else
.     hide item CASE_BREAD_CRUMB
.   endif
. endif

c -------------------------------------
C PEMISSION BASED VISIBILITY
. ifNOTthen ( I_GUI_REC_NEW EQ 1 )
.   hide item PAGE_NEW_KEYS
. endif

end subprocedure
END PROCEDURE
